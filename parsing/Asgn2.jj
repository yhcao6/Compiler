options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(MiniJavaParser)
package myparser;

  import syntaxtree.*;
  public class MiniJavaParser {}
PARSER_END(MiniJavaParser)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN : 
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < DOT: "." >
| < ASSIGN: "=" >
| < LT: "<" >
| < PLUS: "+" >
| < MINUS: "-" >
| < AND : "&" >   
| < NOT : "!" >   
| < BOOLEAN: "boolean" >   
| < CLASS: "class" >
| < INTERFACE: "interface" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < INTEGER: "int" >   
| < LENGTH: "length" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < VOID: "void" >         
| < FOR : "for" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/************************************
 * The MiniJava Grammar Starts Here *
 ************************************/


Program Goal() :
{ MainClass m; 
  ClassDeclList cl = new ClassDeclList();
  ClassDecl c;
}
{
  m=MainClass() 
  ( c=ClassDeclaration() { cl.addElement(c); } )*
  <EOF>
  { return new Program(m,cl); }
}

MainClass MainClass() :
{ Identifier i1,i2;
  Statement s; }
{
  "class" i1=Identifier() "{"
    "public" "static" "void" "main" "(" "String" "[" "]" i2=Identifier() ")"   
    "{" s=Statement() "}"
  "}"
  { return new MainClass(i1,i2,s); }
}

ClassDecl ClassDeclaration() :  
{ ClassDecl c; }
{
  ( 
  LOOKAHEAD(3)
  c=ClassDeclarationSimple()
|
  c=ClassDeclarationExtends()
  )
  { return c; }
}

ClassDecl ClassDeclarationSimple() :
{ Identifier i;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  MethodDecl m;
  MethodDeclList ml = new MethodDeclList();
}
{
  "class" i=Identifier() "{" ( v=VarDeclaration()    { vl.addElement(v); } )* 
                             ( m=MethodDeclaration() { ml.addElement(m); } )* 
                       "}"
  { return new ClassDeclSimple(i,vl,ml); }
}

ClassDecl ClassDeclarationExtends() :
{ Identifier i1,i2;
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  MethodDecl m;
  MethodDeclList ml = new MethodDeclList();
}
{
  "class" i1=Identifier() "extends" i2=Identifier()
                       "{"   ( v=VarDeclaration()    { vl.addElement(v); } )* 
                             ( m=MethodDeclaration() { ml.addElement(m); } )* 
                       "}"
  { return new ClassDeclExtends(i1,i2,vl,ml); }
}

VarDecl VarDeclaration() :  
{ Type t;
  Identifier i;
}
{ 
  t=Type() i=Identifier() ";"      
  { return new VarDecl(t,i); }
}

MethodDecl MethodDeclaration() :  
{ 
  Type t;
  Identifier i;
  FormalList fl = new FormalList();
  VarDecl v;
  VarDeclList vl = new VarDeclList();
  Statement s;
  StatementList sl = new StatementList();
  Exp e; 
}
{
  "public" t=Type() i=Identifier() 
  try{
    "(" ( fl=FormalParameterList() )? ")"   
  } catch (ParseException ex){
    System.out.println("At line " + token.beginLine + ", column " + token.beginColumn + " invalid formal parameter list");
    Token tt;
    do {
      tt = getNextToken();
    } while (tt.kind != RPAREN);
  }
  "{" ( LOOKAHEAD(2) v=VarDeclaration() { vl.addElement(v); } )* 
      ( s=Statement() { sl.addElement(s); } )* "return" e=Expression() ";" "}"
  { return new MethodDecl(t,i,fl,vl,sl,e); }
}

FormalList FormalParameterList() :
{ FormalList fl = new FormalList(); 
  Formal f;
}
{
  f=FormalParameter() { fl.addElement(f); }
  ( f=FormalParameterRest() { fl.addElement(f); } )*
  { return fl; }
}

Formal FormalParameter() :
{ Type t; 
  Identifier i;
  int bc = token.beginColumn;
}
{
  LOOKAHEAD(2) t=Type() i=Identifier() {return new Formal(t,i);}|
  LOOKAHEAD(2) t=Type() 
  {System.out.println("At line " + token.beginLine + ", column " + (token.beginColumn + token.image.length()) + " identifier expected");
   return new Formal(t);} | 
   i=Identifier() 
  {System.out.println("At line " + token.beginLine + ", column " + (bc-1) + " identifier expected");
   return new Formal(i);} 
}

Formal FormalParameterRest() :
{ Formal f; int bc = token.beginColumn;}
{
  ("," f=FormalParameter() 
  | f=FormalParameter() 
    {System.out.println("At line " + token.beginLine + ", column " + (bc + 1) + " ,expected");}
  )
  { return f; }
}

Type Type() :
{ Type t; }
{
  (
  LOOKAHEAD(3)
  t=ArrayType()
|
  t=BooleanType()
|
  t=IntegerType()
|
  t=IdentifierType()     
  )
  { return t; }
}

Type ArrayType() :
{}
{
  "int" "[" "]"
  { return new IntArrayType(); }
}

Type BooleanType() :
{}
{
  "boolean"
  { return new BooleanType(); }
}

Type IntegerType() :
{}
{
  "int"
  { return new IntegerType(); }
}

Type IdentifierType() :
{ Token t; }
{
  t=<IDENTIFIER>
  { return new IdentifierType(t.image); }
}

Statement Statement() :
{ Statement s; }
{
  (
  LOOKAHEAD ("for")
  s=ForLoopStatement()
|
  s=Block()
| 
  LOOKAHEAD(2)
  s=AssignmentStatement()
|
  LOOKAHEAD(2)
  s=ArrayAssignmentStatement()
|
  s=IfStatement()
|
  s=WhileStatement()
|
  s=PrintStatement()
  )
  { return s; }
}

Statement ForLoopStatement() :
{
  Statement s1, s2, s3;
  Exp e;
}
{
  "for" "(" s1=ForInitStatement() ";" e=Expression() ";" s2=StmtExprListStatement() ")" s3=Statement()
  { return new ForLoop(e, s1, s2, s3); }
}

Statement ForInitStatement() :
{
  Statement s;
}
{
  ( LOOKAHEAD(2) s=LocalVariableDeclearationStatement()
  | s=StmtExprListStatement())
  { return s; }
}

Statement StmtExprListStatement() :
{ 
  StatementList sl = new StatementList(); 
  Statement s;
}
{
  (s=StmtExprStatement() {sl.addElement(s);} 
  ("," s=StmtExprStatement() {sl.addElement(s);})*)
  {return new StmtExprList(sl);}
}

Statement StmtExprStatement() :
{
  Identifier i;
  Exp e1, e2;
  Statement s;
}
{
  (LOOKAHEAD(2) i=Identifier() "=" e1=Expression() {s=new StmtExpr(i, e1);}
  | i=Identifier() "[" e1=Expression() "]" "=" e2=Expression() {s=new StmtExpr(i, e1, e2);})
  { return s; }
}

Statement LocalVariableDeclearationStatement ():
{ StatementList sl = new StatementList(); 
  Identifier i;
  Type t;
  Exp e;
}
{
  (t=Type() i=Identifier() "=" e=Expression() {sl.addElement(new LocalVariableDeclaration(i, e));}
  ( "," i=Identifier() "=" e=Expression() {sl.addElement(new LocalVariableDeclaration(i, e));})*)
  { return new LVD(t, sl); }
}

Statement Block() :
{ 
  Statement s;
  StatementList sl = new StatementList(); 
}
{
  "{" ( s=Statement() { sl.addElement(s); } )* "}"
  { return new Block(sl); }
}

Statement AssignmentStatement() :
{ Identifier i;
  Exp e; 
}
{
  i=Identifier() "=" e=Expression() ";"
  { return new Assign(i,e); }
}

Statement ArrayAssignmentStatement() :
{ Identifier i;
  Exp e1,e2;
}
{
  i=Identifier() "[" e1=Expression() "]" "=" e2=Expression() ";"
  { return new ArrayAssign(i,e1,e2); }
}

Statement IfStatement() :
{ Exp e;
  Statement s1,s2;
}
{
  "if" "(" e=Expression() ")" s1=Statement() "else" s2=Statement() 
  { return new If(e,s1,s2); }
}

Statement WhileStatement() :
{ Exp e;
  Statement s;
}
{
  "while" "(" e=Expression() ")" s=Statement() 
  { return new While(e,s); }
}

Statement PrintStatement() :
{ Exp e; }
{
  "System.out.println" "(" e=Expression() ")" ";"
  { return new Print(e); }
}

Exp Expression() :
{ Exp e; }
{
  e=P3Expression()
  { return e; }
}

ExpList ExpressionList() :
{ Exp e1,e2;
  ExpList el = new ExpList();
}
{
  e1=Expression() 
  { el.addElement(e1); }
  ( e2=ExpressionRest() { el.addElement(e2); })*
  { return el; }
}

Exp ExpressionRest() :
{ Exp e; }
{
  "," e=Expression()
  { return e; }
}

// Or
Exp P3Expression() :
{ Exp e1,e2; } 
{
  e1=P4Expression() 
  (LOOKAHEAD(2) "||" e2=P4Expression() {e1=new Or(e1, e2);})*
  { return e1; }
}

// And 
Exp P4Expression() :
{ Exp e1,e2; } 
{
  e1=P9Expression() 
  (LOOKAHEAD(2) "&&" e2=P9Expression() {e1=new And(e1, e2);})*
  { return e1; }
}

// Compare, smaller than
Exp P9Expression() :
{ Exp e1,e2; } 
{
  (e1=P11Expression()
  (LOOKAHEAD(2) "<" e2=P11Expression() {e1=new LessThan(e1,e2);})?)
  { return e1; }
}

// Plus, Minus
Exp P11Expression() :
{ Exp e1, e2; }
{
  (e1=P12Expression()
  (LOOKAHEAD(2) ("+" e2=P12Expression() {e1=new Plus(e1, e2);}
  | "-" e2=P12Expression() {e1=new Minus(e1, e2);}))*)
  { return e1; }
}

// Times, IntegerDivision
Exp P12Expression() :
{ Exp e1, e2; }
{
  (e1=P125Expression()
  (LOOKAHEAD(2) ("*" e2=P125Expression() {e1=new Times(e1, e2);}
  | "/" e2=P125Expression() {e1=new IntegerDivision(e1, e2);}))*)
  { return e1; }
}

Exp P125Expression() :
{ Exp e1, e2; }
{
  (e1=P14Expression() 
  (LOOKAHEAD(2) "^^" e2=P125Expression() {e1=new Exponential(e1, e2);})*)
  { return e1; }
}

// UnaryMinus
Exp P14Expression() :
{ Exp e; }
{
  (LOOKAHEAD(2)("-" e=P14Expression(){e=new UnaryMinus(e);})
  |("!" e=P14Expression(){e=new Not(e);})
  |e=P16Expression())
  { return e; }
}

// ArrayLookup, ArrayLength, MessageSend
Exp P16Expression() :
{Exp e1, e2; Identifier i; ExpList el = new ExpList();}
{
  (e1=PrimaryExpression()
  ( LOOKAHEAD(2)
  ( "[" e2=Expression() "]" {e1=new ArrayLookup(e1, e2);}
  | LOOKAHEAD(2) "." "length" {e1=new ArrayLength(e1);}
  | "." i=Identifier() "(" ( el=ExpressionList() )? ")" {e1=new Call(e1, i, el);}
  ))*)
  { return e1; }
}

Exp PrimaryExpression() :
{ Exp e; }
{ 
  (
  e=IntegerLiteral()
|
  e=TrueLiteral()
|
  e=FalseLiteral()
|
  e=IdentifierExp()
|
  e=ThisExpression()
|
  LOOKAHEAD(3)
  e=ArrayAllocationExpression()
|
  e=AllocationExpression()
// |   
//   e=NotExpression()
|   
  e=BracketExpression()
  )
  { return e; }
}

Exp IntegerLiteral() :
{ Token t; }
{
  t=<INTEGER_LITERAL>
  { return new IntegerLiteral(Integer.parseInt(t.image)); }
}


Exp TrueLiteral() :
{}
{
  "true"
  { return new True(); }
}

Exp FalseLiteral() :
{}
{
  "false"
  { return new False(); }
}

Exp IdentifierExp() :
{ Token t; }
{
  t=<IDENTIFIER>
  { return new IdentifierExp(t.image); }
}

Exp ThisExpression() :
{}
{
  "this"
  { return new This(); }
}

Exp ArrayAllocationExpression() :
{ Exp e; }
{
  "new" "int" "[" e=Expression() "]"
  { return new NewArray(e); }
}

Exp AllocationExpression() :
{ Identifier i; }
{
  "new" i=Identifier() "(" ")"
  { return new NewObject(i); }
}

Exp NotExpression() :
{ Exp e; }
{
  "!" e=Expression() 
  { return new Not(e); }
}

Exp BracketExpression() :
{ Exp e; }
{
  "(" e=Expression() ")"
  { return e; }
}

Identifier Identifier() :
{ Token t; }
{
  t=<IDENTIFIER>
  { return new Identifier(t.image); }
}

